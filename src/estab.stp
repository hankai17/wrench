%{
	#include <net/tcp.h>
	#include <linux/rtc.h>
	#include <linux/timekeeping.h>

	struct stap_info;
%}

probe estab.Estab = 
    kernel.statement("tcp_rcv_state_process@net/ipv4/tcp_input.c:6716")
{
	if (mem_is_stop()) next
	if (!filter($sk)) next
	if (!take_sample()) next

	if (mem_alloc($sk)) {
		next
	}

	do_Estab($sk)
}

function do_Estab(sk:long)
%{
	struct sock *sk = (struct sock *)STAP_ARG_sk;
	struct inet_connection_sock *icsk = inet_csk(sk);
	struct tcp_sock *tp = tcp_sk(sk);
	struct stap_info *info = sk->sk_user_data;
	struct stap_cong *cong;
    struct timespec64 ts;
	unsigned long time;

	if (!info)
		return;

	info->estab_t = tcp_jiffies32;
	info->isn = tp->snd_nxt;
	info->rtx_synack = tp->total_retrans;
	atomic_set(&info->freeing, 0); 

	ktime_get_ts64(&ts);
	time = ts.tv_sec + 8 * 3600;
	info->tm = rtc_ktime_to_tm(time);

	cong = info->cong;
	if (cong && (stap_options.bitmap & STAPF_ADCONG)) {
		cong->adcong_flag = 1;
		atomic_set(&cong->adcong_cnt, 0);
	}
%}

probe estab.Accept = 
	kernel.function("inet_csk_accept").return
{
	if (mem_is_stop()) next
	sk = $return
	if (!sk) next
	
	do_Accept(sk)
}

function do_Accept(sk:long)
%{
	struct sock *sk = (struct sock *)STAP_ARG_sk;
	struct tcp_sock *tp = tcp_sk(sk);
	struct stap_info *info = sk->sk_user_data;

	if (!info)
		return;

	info->accept_wait = jiffies_to_msecs(tcp_jiffies32 - info->estab_t);
	info->init_cwnd = tp->snd_cwnd;
	info->init_ssthr = tp->snd_ssthresh;
%}

